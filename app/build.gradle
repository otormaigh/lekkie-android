/*
 * Copyright (C) 2018 Elliot Tormey
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'
if (file('../enc.properties').exists()) {
  apply from: '../enc.properties'
}

android {
  if (file('../signing/play.json').exists()) {
    playAccountConfigs {
      release {
        jsonFile = file("../signing/play.json")
      }
    }
  }

  compileSdkVersion 28
  defaultConfig {
    applicationId "ie.pennylabs.lekkie"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 6
    versionName "0.2.0"
    archivesBaseName = "lekkie-$versionName"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    buildConfigField "String", "BASE_URL", project.properties["base_url"]

    if (file('../signing/play.json').exists()) {
      playAccountConfig = playAccountConfigs.release
    }

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  signingConfigs {
    debug {
      storeFile file('../signing/debug.keystore')
    }
    if (file('../signing/release.keystore').exists()) {
      release {
        storeFile file(Keystore.RELEASE_STORE)
        storePassword Keystore.STORE_PASSWORD
        keyAlias Keystore.KEY_ALIAS
        keyPassword Keystore.KEY_PASSWORD
      }
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
      applicationIdSuffix '.debug'
      manifestPlaceholders = [google_maps_key: project.properties["debug_map_key"]]
    }

    release {
      if (file('../signing/release.keystore').exists()) {
        signingConfig signingConfigs.release
      } else {
        signingConfig signingConfigs.debug
      }
      postprocessing {
        proguardFiles 'proguard-rules.pro'
        removeUnusedResources = true
        removeUnusedCode = true
        optimizeCode = true
        obfuscate = true
      }
      manifestPlaceholders = [google_maps_key: project.properties["release_map_key"]]
    }
  }

  packagingOptions {
    exclude 'META-INF/main.kotlin_module'
  }

  lintOptions {
    lintConfig file("../quality/lint-config.xml")
  }
}

play {
  track = 'internal'
  untrackOld = true
  uploadImages = true
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Deps.kotlin"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$Deps.coroutines"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$Deps.coroutines"

  implementation 'com.google.android.material:material:1.0.0-alpha3'
  implementation 'androidx.appcompat:appcompat:1.0.0-alpha3'
  implementation 'androidx.recyclerview:recyclerview:1.0.0-alpha3'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
  implementation "androidx.lifecycle:lifecycle-livedata:2.0.0-alpha1"
  implementation "androidx.room:room-runtime:$Deps.arch_room"
  implementation 'com.google.android.gms:play-services-maps:15.0.1'
  kapt "androidx.room:room-compiler:$Deps.arch_room"
  implementation 'androidx.core:core-ktx:1.0.0-alpha3'

  implementation 'com.github.otormaigh:conductor-dagger:c918a23'
  implementation "com.google.dagger:dagger:$Deps.dagger"
  implementation "com.google.dagger:dagger-android:$Deps.dagger"
  kapt "com.google.dagger:dagger-compiler:$Deps.dagger"
  kapt "com.google.dagger:dagger-android-processor:$Deps.dagger"

  implementation "com.squareup.retrofit2:retrofit:$Deps.retrofit2"
  implementation "com.squareup.retrofit2:converter-moshi:$Deps.retrofit2"
  implementation "com.squareup.okhttp3:okhttp:$Deps.okhttp3"
  implementation "com.squareup.okhttp3:logging-interceptor:$Deps.okhttp3"
  implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.11.0'
  debugImplementation "com.squareup.okhttp3:mockwebserver:$Deps.okhttp3"

  implementation 'com.bluelinelabs:conductor:2.1.4'
  implementation 'com.jakewharton.timber:timber:4.7.0'
  implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

  implementation 'com.google.firebase:firebase-analytics:16.0.1'
  implementation 'com.google.firebase:firebase-core:16.0.1'
  implementation 'com.google.firebase:firebase-perf:16.0.0'
  implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

  testImplementation 'junit:junit:4.12'
  testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
  testImplementation("org.threeten:threetenbp:1.3.6") {
    exclude group: "com.jakewharton.threetenabp", module: "threetenabp"
  }
  androidTestImplementation 'androidx.test:runner:1.1.0-alpha3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha3'
}

check.dependsOn rootProject.detektCheck

task prepareNextRelease {
  description "Prepares the project for the next release version, see: RELEASING.md"

  doLast {
    def new_version = project.android.defaultConfig.versionName

    // 7. Create a new release branch `git checkout -b release-{versionName}`
    exec { commandLine 'git', 'checkout', '-b', "release-$new_version" }

    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    exec {
      def short_hash = stdout.toString().take(7) // make sure there's no unwanted chars

      // 8. Update previous `CHANGELOG.md` entry to append title with the build commit of that release
      commandLine 'sed', '-i', "", '-e', "1 s/\$/ - $short_hash/", file('../CHANGELOG.md')
    }

    // 9. Commit changes `git commit -am 'bump version to {versionName}'`
    exec { commandLine 'git', 'commit', '-am', "bump version to $new_version" }
  }
}
kotlin {
  experimental {
    coroutines 'enable'
  }
}

kapt {
  useBuildCache = true
}

androidExtensions {
  experimental = true
}

if (file('google-services.json').exists()) {
  apply plugin: 'io.fabric'
  apply plugin: 'com.google.firebase.firebase-perf'
  apply plugin: 'com.google.gms.google-services'
}